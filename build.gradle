import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

buildscript {
    repositories {
        maven { url 'https://cache-redirector.jetbrains.com/intellij-repository/snapshots' }
        mavenCentral()
    }

    dependencies {
        classpath("com.jetbrains.rd:rd-gen:2026.1.2") // https://central.sonatype.com/artifact/com.jetbrains.rd/rd-gen
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.3.10'
    id 'org.jetbrains.intellij.platform' version '2.10.5'
    id 'me.filippov.gradle.jvm.wrapper' version '0.15.0'
}

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

group 'com.reallyliri.plugins'
version '1.10'
def pluginSinceBuild = '261'

processResources {
    filesMatching('META-INF/plugin.xml') {
        expand(
            pluginVersion: project.version,
            pluginSinceBuild: pluginSinceBuild
        )
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
    }
}

dependencies {
    intellijPlatform {
        rider('2025.3.2') // https://www.jetbrains.com/updates/updates.xml
    }
}

publishPlugin {
    token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
}
